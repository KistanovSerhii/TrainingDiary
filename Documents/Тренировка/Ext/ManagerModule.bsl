
Функция ПолучитьКрайнееФИО() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Тренировка.Спортсмен КАК Ссылка
		|ИЗ
		|	Документ.Тренировка КАК Тренировка
		|ГДЕ
		|	Тренировка.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Тренировка.Дата УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка
	КонецЕсли;
	
	Возврат Справочники.Спортсмен.ПустаяСсылка();
	
КонецФункции

Функция ПолучитьКрайнееВремяВосстановления() Экспорт
	
	Ответ	= Новый Массив;
	Запрос 	= Новый Запрос;
	
	Запрос.Текст =
	
#Область ТекстЗапроса
		"ВЫБРАТЬ
		|	ТНаборУпражнений.Стиль КАК Стиль,
		|	ТНаборУпражнений.Упражнение КАК Упражнение,
		|	ТНаборУпражнений.ГруппаМышц КАК ГруппаМышц
		|ПОМЕСТИТЬ ВТНаборУпражнений
		|ИЗ
		|	РегистрСведений.КонтрольВосстановления КАК ТНаборУпражнений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ТренировкаЖурналТренировок.НомерСтроки) КАК НомерСтроки,
		|	ТренировкаЖурналТренировок.Стиль КАК Стиль,
		|	ТренировкаЖурналТренировок.Ссылка КАК Ссылка,
		|	ТНаборУпражнений.ГруппаМышц КАК ГруппаМышц
		|ПОМЕСТИТЬ ВТЖурналТренировок
		|ИЗ
		|	Документ.Тренировка.ЖурналТренировок КАК ТренировкаЖурналТренировок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНаборУпражнений КАК ТНаборУпражнений
		|		ПО ТренировкаЖурналТренировок.Упражнение = ТНаборУпражнений.Упражнение
		|			И ТренировкаЖурналТренировок.Стиль = ТНаборУпражнений.Стиль
		|
		|СГРУППИРОВАТЬ ПО
		|	ТренировкаЖурналТренировок.Стиль,
		|	ТренировкаЖурналТренировок.Ссылка,
		|	ТНаборУпражнений.ГруппаМышц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТТренировка.Дата КАК Период,
		|	ТЖурналТренировок.НомерСтроки КАК НомерСтроки,
		|	ТЖурналТренировок.Стиль КАК Стиль,
		|	ТЖурналТренировок.Ссылка КАК Ссылка,
		|	ТЖурналТренировок.ГруппаМышц КАК ГруппаМышц
		|ПОМЕСТИТЬ ВТТренировка
		|ИЗ
		|	ВТЖурналТренировок КАК ТЖурналТренировок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Тренировка КАК ТТренировка
		|		ПО ТЖурналТренировок.Ссылка = ТТренировка.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Тренировка.Период) КАК Период,
		|	Тренировка.Стиль КАК Стиль,
		|	ВТТренировка.ГруппаМышц КАК ГруппаМышц
		|ПОМЕСТИТЬ ВТФиксация
		|ИЗ
		|	РегистрНакопления.Тренировка КАК Тренировка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТренировка КАК ВТТренировка
		|		ПО Тренировка.Регистратор = ВТТренировка.Ссылка
		|			И Тренировка.НомерСтроки = ВТТренировка.НомерСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	Тренировка.Стиль,
		|	ВТТренировка.ГруппаМышц
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТТренировка.Период КАК Период,
		|	ТТренировка.Ссылка КАК ТренировкаСсылка,
		|	ТТренировка.Стиль КАК Стиль,
		|	ТТренировка.ГруппаМышц КАК ГруппаМышц
		|ИЗ
		|	ВТТренировка КАК ТТренировка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФиксация КАК ТФиксация
		|		ПО ТТренировка.Период = ТФиксация.Период
		|			И ТТренировка.Стиль = ТФиксация.Стиль
		|УПОРЯДОЧИТЬ ПО
		|	ТТренировка.ГруппаМышц ВОЗР,
		|	ТТренировка.Период УБЫВ";
#КонецОбласти

	РезультатЗапроса 	= Запрос.Выполнить();	
	Выборка 			= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОтветСтруктура = Новый Структура;
		ОтветСтруктура.Вставить("ТренировкаСсылка");
		ОтветСтруктура.Вставить("ГруппаМышц");
		ОтветСтруктура.Вставить("Стиль");		
		ОтветСтруктура.Вставить("Дни");
		
		ОтветСтруктура.ТренировкаСсылка	= Выборка.ТренировкаСсылка;
		ОтветСтруктура.ГруппаМышц		= Выборка.ГруппаМышц;
		ОтветСтруктура.Стиль			= Строка(Выборка.Стиль);
		РазницаВДнях 					= (НачалоДня(ТекущаяДата()) - НачалоДня(Выборка.Период)) / (60 * 60 * 24);  
		ОтветСтруктура.Дни 				= РазницаВДнях;
		
		Ответ.Добавить(ОтветСтруктура);
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции

Процедура Печать(ТабДок, Ссылка) Экспорт
	//{{_КОНСТРУКТОР_ПЕЧАТИ(Печать)
	Макет = Документы.Тренировка.ПолучитьМакет("Печать");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	тТренировка.Ссылка,
	|	тТренировка.Дата,
	|	тТренировка.Спортсмен,
	|	тЖурналТренировок.НомерСтроки,
	|	тЖурналТренировок.Упражнение,
	|	тЖурналТренировок.Стиль,
	|	тЖурналТренировок.Повторы,
	|	тЖурналТренировок.Вес
	|ИЗ
	|	Документ.Тренировка.ЖурналТренировок КАК тЖурналТренировок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Тренировка КАК тТренировка
	|	ПО тЖурналТренировок.Ссылка = тТренировка.Ссылка
	|ГДЕ
	|	тТренировка.Ссылка В (&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	тЖурналТренировок.Упражнение ВОЗР,
	|	тЖурналТренировок.Стиль ВОЗР,
	|	тЖурналТренировок.НомерСтроки ВОЗР";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок 					= Макет.ПолучитьОбласть("Заголовок");
	Шапка 								= Макет.ПолучитьОбласть("Шапка");
	ОбластьЖурналТренировокШапка 		= Макет.ПолучитьОбласть("ЖурналТренировокШапка");
	ОбластьЖурналТренировок 			= Макет.ПолучитьОбласть("ЖурналТренировок");
	ОбластьЖурналТренировокРазделитель 	= Макет.ПолучитьОбласть("ЖурналТренировокРазделитель");
	
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	ТекущияСсылка				= Неопределено;
	ТекущееУпражнение			= Неопределено;
	ТекущийСтиль				= Неопределено;
	ТекущийНомерСтроки			= 0;
	БылВыводОбластьДляТекДок	= Ложь;
	
	Пока Выборка.Следующий() Цикл
		#Область новыйДок
		Если ТекущияСсылка <> Выборка.Ссылка Тогда
			Если ВставлятьРазделительСтраниц Тогда
				ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
				ВставлятьРазделительСтраниц = Ложь;
			КонецЕсли;			
			ТабДок.Вывести(ОбластьЗаголовок);

			Шапка.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(Шапка, Выборка.Уровень());
			ТабДок.Вывести(ОбластьЖурналТренировокШапка);			
		КонецЕсли;
		#КонецОбласти

		Если ТекущееУпражнение <> Выборка.Упражнение ИЛИ ТекущийСтиль <> Выборка.Стиль Тогда
			ТекущееУпражнение 	= Выборка.Упражнение;
			ТекущийСтиль 		= Выборка.Стиль;			
			ТекущийНомерСтроки 	= 0;
			
			Если БылВыводОбластьДляТекДок Тогда
				ТабДок.Вывести(ОбластьЖурналТренировокРазделитель);
			КонецЕсли;
		КонецЕсли;
		
		ОбластьЖурналТренировок.Параметры.Заполнить(Выборка);
		ТекущийНомерСтроки								= ТекущийНомерСтроки + 1;
		ОбластьЖурналТренировок.Параметры.НомерСтроки 	= ТекущийНомерСтроки;
		
		ТабДок.Вывести(ОбластьЖурналТренировок, Выборка.Уровень());
		БылВыводОбластьДляТекДок = Истина;
		
		#Область ПереходНаСледующийДок
		Если ТекущияСсылка <> Выборка.Ссылка Тогда
			ВставлятьРазделительСтраниц = Истина;
			ТекущияСсылка 				= Выборка.Ссылка;
			БылВыводОбластьДляТекДок	= Ложь;
		КонецЕсли;
		#КонецОбласти
	КонецЦикла;
	//}}
КонецПроцедуры
